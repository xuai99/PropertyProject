apply plugin: 'com.android.application'
apply plugin: 'findbugs'

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "protect.rentalcalc"
        minSdkVersion 23
        targetSdkVersion 27
        versionCode 6
        versionName "0.5.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        disable "MissingTranslation"
    }

    // Starting with Android Studio 3 Robolectric is unable to find resources.
    // The following allows it to find the resources.
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

// On Travis-CI the unit tests consume too much memory. This may be
// due to a leak when running Robolectric tests. To isolate the
// impact of potential leaks, each test is run in its own process.
android.testOptions.unitTests.all {
    maxParallelForks = 1
    forkEvery = 1
}

dependencies {
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:27.0.2'
    implementation 'com.android.support:design:27.0.2'
    implementation group: 'com.google.guava', name: 'guava', version: '20.0'
    implementation 'io.github.kexanie.library:MathView:0.0.6'
    implementation 'com.github.bumptech.glide:glide:3.8.0'
    implementation 'com.github.apl-devs:appintro:v4.2.0'
    testImplementation 'junit:junit:4.12'
    testImplementation "org.robolectric:robolectric:3.3.2"

    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'
    implementation 'com.google.android.gms:play-services-maps:16.0.0'



}

findbugs {
    sourceSets = []
    ignoreFailures = false
}

task findbugs(type: FindBugs, dependsOn: 'assembleDebug') {

    description 'Run findbugs'
    group 'verification'

    classes = fileTree('build/intermediates/javac/debug/compileDebugJavaWithJavac/classes')
    source = fileTree('src/main/java')
    classpath = files()

    effort = 'max'

    excludeFilter = file("./config/findbugs/exclude.xml")

    reports {
        xml.enabled = false
        html.enabled = true
    }
}
